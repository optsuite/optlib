/-
Copyright (c) 2024 Chenyi Li, Shengyang Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chenyi Li, Shengyang Xu
-/
import Mathlib.Topology.Semicontinuous
import Mathlib.Analysis.InnerProductSpace.EuclideanDist
import Analysis.Calculation
import Analysis.Subgradient
import Analysis.Optimality_Condition_of_Unconstrained_Problem
import Analysis.Lsmooth
import Analysis.Stongly

/-!
 proximal operator
-/
noncomputable section

open Set InnerProductSpace Bornology Topology Filter TopologicalSpace

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E]
variable [CompleteSpace E] [ProperSpace E]

variable {x y yâ‚ yâ‚‚: E} {s : Set E} {f : E â†’ â„} {nes : Nonempty s}

/-
  a point satisfies the proximal property if it is a minimizer of the function f(u)+1/2â€–u-xâ€–Â²
-/
def prox_prop (f : E â†’ â„) (x : E) (xm : E) : Prop :=
  IsMinOn (fun u â†¦ f u + â€–u - xâ€– ^ 2 / 2) univ xm

/-
  the set of all points that satisfy the proximal property
-/
def prox_set (f : E â†’ â„) (x : E) : Set E := {u | prox_prop f x u}

/-
  if the proximal set is nonempty then we can choose the point that satisfies the proximal property
-/
def prox_point (f : E â†’ â„) (x : E) (h : âˆƒ y, prox_set f x y) : E :=
  Classical.choose h

lemma strongconvex_of_convex_add_sq (f : E â†’ â„) (x : E) (hfun : ConvexOn â„ univ f) :
    StrongConvexOn univ (1 : â„) fun u â†¦ f u + â€–u - xâ€– ^ 2 / 2 := by
  rw [ConvexOn] at hfun
  rcases hfun with âŸ¨conv, hfunâŸ©
  rw [StrongConvexOn, UniformConvexOn]
  constructor
  Â· exact conv
  Â· intro y yin z zin a b anneg bnneg absum1; simp
    rw [mul_add, add_assoc, add_comm (a * (â€–y - xâ€– ^ 2 / 2)), â† add_assoc]
    rw [mul_add, â† add_assoc, â† add_sub _ (a * (â€–y - xâ€– ^ 2 / 2)), add_assoc]
    apply add_le_add
    Â· rw [â† smul_eq_mul, â† smul_eq_mul]
      apply hfun yin zin anneg bnneg absum1
    Â· field_simp; rw [div_le_div_right, add_sub]
      have eq1 : a â€¢ y + b â€¢ z - x = a â€¢ (y - x) + b â€¢ (z - x) := by
        rw [smul_sub, smul_sub, add_comm_sub, sub_sub, â† add_smul, add_comm b a]
        rw [absum1, one_smul, â† add_sub]
      have eq2 (u v : E) : â€–a â€¢ u + b â€¢ vâ€– ^ 2 = b * â€–vâ€– ^ 2
        + a * â€–uâ€– ^ 2 - a * b * â€–u - vâ€– ^ 2 := by
        rw [norm_add_sq_real, norm_sub_sq_real]
        rw [inner_smul_left, inner_smul_right, norm_smul, norm_smul]; field_simp
        rw [add_comm (b * â€–vâ€– ^ 2), mul_pow, sq_abs, mul_pow, sq_abs]
        rw [mul_add, â† sub_sub, mul_sub, â† sub_add]
        rw [add_sub_right_comm, add_sub_right_comm, â† sub_mul, â† add_sub, â† sub_mul]
        nth_rw 3 [â† mul_one a]; rw [â† absum1, mul_add]
        nth_rw 5 [â† mul_one b]; rw [â† absum1, mul_add, mul_comm b a]
        rw [pow_two, pow_two b]; simp; rw [add_right_comm, add_left_cancel_iff]
        rw [mul_mul_mul_comm, mul_comm a 2, mul_assoc]
      have eq3 : y - z = (y - x) - (z - x) := by simp
      have eq4 (u v : E) : â€–a â€¢ u + b â€¢ vâ€– ^ 2 â‰¤ b * â€–vâ€– ^ 2
        + a * â€–uâ€– ^ 2 - a * b * â€–u - vâ€– ^ 2 := by rw [eq2]
      let u := y - x
      let v := z - x
      rw [eq1, eq3];
      show â€–a â€¢ u + b â€¢ vâ€– ^ 2 â‰¤ b * â€–vâ€– ^ 2 + a * â€–uâ€– ^ 2 - a * b * â€–u - vâ€– ^ 2
      apply eq4 u v
      simp

lemma gradient_of_sq : âˆ€ u : E, HasGradientAt (fun u â†¦ â€–u - xâ€– ^ 2 / 2) (u - x) u := by
  intro s
  apply Convergence_HasGradient
  simp; intro e ep; use e
  constructor
  Â· linarith
  Â· intro x' dles; field_simp; rw [abs_div]; simp
    have eq1 (u v : E) (e : â„) (dle : â€–u - vâ€– â‰¤ e) :
      |â€–vâ€– ^ 2 - â€–uâ€– ^ 2 - inner ((2 : â„) â€¢ u) (v - u)| â‰¤ e * â€–u - vâ€– := by
      rw [â† norm_neg (u - v), neg_sub] at dle;
      rw [â† real_inner_self_eq_norm_sq, â† real_inner_self_eq_norm_sq, inner_sub_right]
      rw [real_inner_smul_left, real_inner_smul_left]; ring_nf
      rw [add_sub, add_sub_right_comm, mul_two, â† sub_sub]
      rw [â† inner_sub_left, sub_add, â† inner_sub_right]
      rw [real_inner_comm, â† inner_sub_left, real_inner_self_eq_norm_sq]
      rw [abs_of_nonneg, pow_two, â† norm_neg (u - v), neg_sub]
      apply mul_le_mul_of_nonneg_right dle (norm_nonneg (v - u))
      apply pow_two_nonneg
    let u := s - x
    let v := x' - x
    rw [â† real_inner_smul_left]
    have eq2 : s - x' = u - v := by simp
    have eq3 : x' - s = v - u := by simp
    rw [eq2, eq3]
    show |â€–vâ€– ^ 2 - â€–uâ€– ^ 2 - inner ((2 : â„) â€¢ u) (v - u)| / 2 â‰¤ e * â€–u - vâ€–
    calc
      |â€–vâ€– ^ 2 - â€–uâ€– ^ 2 - inner ((2 : â„) â€¢ u) (v - u)| / 2 â‰¤ (e * â€–u - vâ€–) / 2 := by
        rw [div_le_div_right]
        apply eq1; simp; apply dles; simp
      _ â‰¤ e * â€–u - vâ€– := by
        field_simp

lemma bounded_lowersemicontinuous_to_epi_closed (f : E â†’ â„) (hc : LowerSemicontinuousOn f univ)
    (underboundf : âˆƒ b : â„, âˆ€ x : E, b â‰¤ f x) :
    IsClosed {p : (E Ã— â„) | f p.1 â‰¤ p.2} := by
  apply IsSeqClosed.isClosed
  intro xn p xnin xntend
  simp
  have xncond : âˆ€ (n : â„•), f (xn n).1 â‰¤ (xn n).2 := by
    intro n; specialize xnin n; simp at xnin
    exact xnin
  rw [Prod.tendsto_iff] at xntend
  rcases xntend with âŸ¨xtend, ytendâŸ©
  rw [LowerSemicontinuousOn] at hc
  specialize hc p.1
  simp at hc; rw [LowerSemicontinuousWithinAt, nhdsWithin_univ] at hc
  let linf := liminf (fun n â†¦ f (xn n).1) atTop
  have aux : Tendsto (fun n â†¦ (xn n).2) atTop (nhds p.2) â†”
        âˆ€ Îµ > 0, âˆƒ N, âˆ€ n â‰¥ N, (fun n â†¦ (xn n).2) n âˆˆ Ioo (p.2 - Îµ) (p.2 + Îµ) := by
      have : atTop.HasBasis (fun _ : â„• â†¦ True) Ici := atTop_basis
      rw [this.tendsto_iff (nhds_basis_Ioo_pos p.2)]
      simp
  have ieq1 : f p.1 â‰¤ linf := by
    by_contra h; push_neg at h
    let t := (linf + f p.1) / 2
    have tin : t < f p.1 := add_div_two_lt_right.2 h
    specialize hc t tin
    have ieq2 : t â‰¤ linf := by
      apply le_liminf_of_le
      Â· rw [Filter.IsCoboundedUnder, Filter.IsCobounded]
        rcases underboundf with âŸ¨bf, condfâŸ©
        use p.2 + 1; simp; intro a N condb
        rw [aux] at ytend
        specialize ytend 1; simp at ytend
        rcases ytend with âŸ¨N0, supâŸ©
        let M := max N N0
        specialize condb M (le_max_left N N0)
        specialize sup M (le_max_right N N0)
        linarith [condb, xncond M, sup.2]
      Â· let auxle := fun x : E â†¦ (t â‰¤ f x)
        show âˆ€á¶  (n : â„•) in atTop, auxle (xn n).1
        apply Tendsto.eventually xtend
        let auxlt := fun x : E â†¦ (t < f x)
        have le_of_lt : âˆ€ x : E, auxlt x â†’ auxle x := by
          simp; intro x cd; exact le_of_lt cd
        apply Eventually.mono hc le_of_lt
    contrapose! ieq2
    apply left_lt_add_div_two.2 h
  have ieq3 : linf â‰¤ p.2 := by
    have ieq4 : liminf (fun n â†¦ (xn n).2) atTop = p.2 := Tendsto.liminf_eq ytend
    rw [â† ieq4]
    apply liminf_le_liminf
    simp; use 1
    intro b _; exact xncond b
    rw [Filter.IsBoundedUnder, Filter.IsBounded]
    rcases underboundf with âŸ¨bf, condfâŸ©
    use bf; simp; use 0; intro b; simp; exact condf (xn b).1
    rw [Filter.IsCoboundedUnder, Filter.IsCobounded]
    use p.2 + 1; simp; intro a N condb
    rw [aux] at ytend
    specialize ytend 1; simp at ytend
    rcases ytend with âŸ¨N0, supâŸ©
    let M := max N N0
    specialize condb M (le_max_left N N0)
    specialize sup M (le_max_right N N0)
    linarith [condb, sup.2]
  linarith

theorem prox_well_define (f : E â†’ â„) (hc : LowerSemicontinuousOn f univ) :
    âˆ€ x, âˆƒ y, prox_prop f x y := by
  intro x
  rcases nes with âŸ¨z, _âŸ©
  have : Nonempty (SubderivAt f z) := by
    apply SubderivAt.nonempty
  simp at this
  rcases this with âŸ¨a, ainâŸ©
  rw [â† mem_SubderivAt, HasSubgradientAt] at ain
  let g := fun u â†¦ f u + â€–u - xâ€– ^ 2 / 2
  let epi := {p : (E Ã— â„) | g p.1 â‰¤ p.2}
  have second_lower_bound (y : E) : g y â‰¥ f z + inner a (y - z) + â€–y - xâ€– ^ 2 / 2 := by
    simp
    specialize ain y; linarith
  have lower_bound (y : E) : f z + inner a (x - z) - â€–aâ€– ^ 2 / 2 â‰¤ g y := by
    have : y - z = x - z + (y - x) := by simp
    specialize second_lower_bound y
    rw [this, inner_add_right, â† add_assoc, add_assoc] at second_lower_bound
    have : 0 â‰¤ â€–aâ€– ^ 2 / 2 + inner a (y - x) + â€–y - xâ€– ^ 2 / 2 := by
      field_simp; rw [mul_comm, â† norm_add_sq_real]
      apply div_nonneg (sq_nonneg â€–a + (y - x)â€–)
      norm_num
    calc
      f z + inner a (x - z) - â€–aâ€– ^ 2 / 2 â‰¤ f z + inner a (x - z) - â€–aâ€– ^ 2 / 2 +
        (â€–aâ€– ^ 2 / 2 + inner a (y - x) + â€–y - xâ€– ^ 2 / 2) := le_add_of_nonneg_right this
      _ = f z + inner a (x - z) + (inner a (y - x) + â€–y - xâ€– ^ 2 / 2) := by ring
      _ â‰¤ g y := second_lower_bound
  have epi_closed : IsClosed epi := by
    apply bounded_lowersemicontinuous_to_epi_closed
    Â· apply LowerSemicontinuousOn.add hc
      apply lowerSemicontinuousOn_univ_iff.2
      apply Continuous.lowerSemicontinuous
      apply continuous_iff_continuousOn_univ.2
      apply HasGradientAt.continuousOn
      intro u _
      apply gradient_of_sq u
    use (f z + inner a (x - z) - â€–aâ€– ^ 2 / 2)
  let S := {y : E| g y â‰¤ g z + 1}
  let ImS := {g y | y âˆˆ S}
  have neImS : Set.Nonempty ImS := by
    use g z; simp; use z; simp
  have S_bddbelow : BddBelow ImS := by
    use (f z + inner a (x - z) - â€–aâ€– ^ 2 / 2)
    rw [mem_lowerBounds]
    rintro gy âŸ¨y0, _, gyeqâŸ©
    rw [â† gyeq]; exact lower_bound y0
  have compacts : IsCompact (closure S) := by
    let B := Metric.closedBall (x - a) (â€–z - (x - a)â€– + 2)
    have sinb : S âŠ† B := by
      intro u uin
      simp at uin
      apply mem_closedBall_iff_norm.2
      have norm_bound: â€–u - (x - a)â€– â‰¤ â€–z - (x - a)â€– + 2 := by
        have ieq : f z + inner a (u - z) + â€–u - xâ€– ^ 2 / 2 â‰¤ f z + â€–z - xâ€– ^ 2 / 2 + 1 := by
          calc
          f z + inner a (u - z) + â€–u - xâ€– ^ 2 / 2 â‰¤ g u := second_lower_bound u
          _ â‰¤ f z + â€–z - xâ€– ^ 2 / 2 + 1 := uin
        rw [add_assoc, add_assoc, add_le_add_iff_left] at ieq
        have eq : inner a (u - z) + â€–u - xâ€– ^ 2 / 2 =
            (â€–u - (x - a)â€– ^ 2 - â€–aâ€– ^ 2 + 2 * inner (x - z) a) / 2 := by
            field_simp; rw [â† sub_add, norm_add_sq_real]; ring_nf
            rw [add_assoc, â† add_mul, â† inner_add_left, add_comm, real_inner_comm]; simp
        rw [eq] at ieq
        have ieq2 : â€–u - (x - a)â€– ^ 2 â‰¤ â€–z - (x - a)â€– ^ 2 + 2 := by
          field_simp at ieq; rw [div_le_div_right, sub_add, sub_le_iff_le_add] at ieq
          rw [add_right_comm, add_comm (â€–z - xâ€– ^ 2), norm_sub_rev z x] at ieq
          rw [real_inner_comm, â† norm_sub_sq_real, â† sub_add a, sub_add_comm] at ieq
          rw [sub_add] at ieq; exact ieq; norm_num
        have : |â€–z - (x - a)â€– + 2| = â€–z - (x - a)â€– + 2 := by
          apply abs_of_pos; apply add_pos_of_nonneg_of_pos (norm_nonneg (z - (x - a)))
          simp
        rw [â† abs_norm, â† this, â† sq_le_sq, add_sq]
        calc
          â€–u - (x - a)â€– ^ 2 â‰¤ â€–z - (x - a)â€– ^ 2 + 2 := ieq2
          _ â‰¤ â€–z - (x - a)â€– ^ 2 + 2 * â€–z - (x - a)â€– * 2 + 2 ^ 2 := by
            rw [add_assoc, add_le_add_iff_left]; apply le_add_of_nonneg_of_le
            simp; norm_num
      exact norm_bound
    have compactb : IsCompact B := isCompact_closedBall (x - a) (â€–z - (x - a)â€– + 2)
    apply isCompact_closure_of_subset_compact compactb sinb
  rcases exists_seq_tendsto_sInf neImS S_bddbelow with âŸ¨fx, _, cfx, fxsâŸ©
  choose x xsub xeq using fxs
  have xsubs : âˆ€ (n : â„•), x n âˆˆ closure S := by
    intro n; apply subset_closure (xsub n)
  rcases IsCompact.tendsto_subseq compacts xsubs with âŸ¨x', _, k, mono, cxkâŸ©
  have cfxk : Filter.Tendsto (g âˆ˜ x âˆ˜ k) Filter.atTop (ð“ (sInf ImS)) := by
    have xkeq : âˆ€ (n : â„•), (g âˆ˜ x âˆ˜ k) n = (fx âˆ˜ k) n := fun n => xeq <| k n
    rw [tendsto_congr xkeq]
    apply Tendsto.comp cfx (StrictMono.tendsto_atTop mono)
  have inepi : (x', sInf ImS) âˆˆ epi := by
    let p := fun c â†¦ (((fun n â†¦ x n) âˆ˜ k) c, (g âˆ˜ x âˆ˜ k) c)
    have pnin :  âˆ€ c : â„•, p c âˆˆ epi := by simp
    apply IsClosed.isSeqClosed epi_closed pnin
    show Tendsto (fun c â†¦ (((fun n â†¦ x n) âˆ˜ k) c, (g âˆ˜ x âˆ˜ k) c)) atTop (ð“ (x', sInf ImS))
    apply Tendsto.prod_mk_nhds cxk cfxk
  have minima_ieq : g x' â‰¤ sInf ImS := inepi
  have minima : âˆ€ w : E, g x' â‰¤ g w := by
      intro w
      by_cases hw : w âˆˆ S
      Â· have gwin : g w âˆˆ ImS := by use w
        have legw : sInf ImS â‰¤ g w := by
          rw [Real.sInf_le_iff S_bddbelow neImS]
          intro _ epos; use g w; use gwin; linarith
        linarith
      Â· have gwnin : g z + 1 < g w := by
          simp at hw; simp; exact hw
        have gzin : g z âˆˆ ImS := by use z; simp
        have legw : sInf ImS â‰¤ g w := by
          rw [Real.sInf_le_iff S_bddbelow neImS]
          intro _ epos; use g z; use gzin; linarith
        linarith
  use x'; rw [prox_prop, isMinOn_iff]
  intro v _; exact minima v

theorem prox_well_define' (f : E â†’ â„) (x : E) (hfun : ConvexOn â„ univ f)
    (_ : LowerSemicontinuousOn f univ) (hâ‚ : prox_prop f x yâ‚)
    (hâ‚‚ : prox_prop f x yâ‚‚) : yâ‚ = yâ‚‚ := by
  let g := fun u â†¦ f u + â€–u - xâ€– ^ 2 / 2
  let m := (1 : â„)
  have gsconv : StrongConvexOn univ m g := by
    apply strongconvex_of_convex_add_sq
    show ConvexOn â„ univ f; apply hfun
  rw [prox_prop] at hâ‚
  rw [prox_prop] at hâ‚‚
  apply Strongly_Convex_Unique_Minima gsconv
  apply hâ‚; apply hâ‚‚; simp; simp; norm_num

/-
  the definition of the proximal operator for a closed convex function
-/
def prox_point_c (f : E â†’ â„) (x : E) (hc : LowerSemicontinuousOn f univ) : E :=
  have h : âˆƒ y, prox_set f x y := by
    apply prox_well_define f hc x; use s; exact nes
  Classical.choose h

section properties

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E] [CompleteSpace E]

variable {x y yâ‚ yâ‚‚: E} {s : Set E} {f : E â†’ â„} {u : E} {t : â„}

open Set InnerProductSpace

lemma convex_of_norm_sq {s : Set E} (x : E) (conv: Convex â„ s) :
    ConvexOn â„ s (fun (u : E) â†¦ â€–u - xâ€– ^ 2 / 2) := by
  rw [ConvexOn]; use conv
  intro y _ z _ a b anneg bnneg absum1
  field_simp
  have eq1 : a â€¢ y + b â€¢ z - x = a â€¢ (y - x) + b â€¢ (z - x) := by
    rw [smul_sub, smul_sub, add_comm_sub, sub_sub, â† add_smul, add_comm b a]
    rw [absum1, one_smul, â† add_sub]
  rw [eq1]
  have ieq1 (u v : E) : â€–a â€¢ u + b â€¢ vâ€– ^ 2 / 2 â‰¤ (a * â€–uâ€– ^ 2 + b * â€–vâ€– ^ 2) / 2 := by
    rw [div_le_div_right, norm_add_sq_real, add_comm, â† add_assoc]
    rw [norm_smul, norm_smul, mul_pow, mul_pow]; simp
    nth_rw 3 [â† mul_one a]; nth_rw 3 [â† one_mul b]
    rw [â† absum1]; ring_nf; rw [add_right_comm]
    apply add_le_add_right
    rw [add_comm]; apply add_le_add_right
    calc
      inner (a â€¢ u) (b â€¢ v) * 2 â‰¤ â€–a â€¢ uâ€– * â€–b â€¢ vâ€– * 2 := by
        rw [mul_le_mul_right]
        apply real_inner_le_norm
        simp
      _ = a * b * (2 * â€–uâ€– * â€–vâ€–) := by
        rw [norm_smul, norm_smul]; simp
        rw [abs_of_nonneg anneg, abs_of_nonneg bnneg]; ring
      _ â‰¤ a * b * (â€–uâ€– ^ 2 + â€–vâ€– ^ 2) := by
        by_cases a * b > 0
        Â· rw [mul_le_mul_left]
          apply two_mul_le_add_pow_two
          linarith
        Â· have ieq2 : 0 â‰¤ a * b := by apply mul_nonneg anneg bnneg
          have ieq3 : 0 = a * b := by linarith
          rw [â† ieq3]; simp
      _ = b * â€–vâ€– ^ 2 * a + b * a * â€–uâ€– ^ 2 := by ring
    simp
  apply ieq1

lemma Subderivat_eq_SubderivWithinAt_univ (f : E â†’ â„) :
    SubderivWithinAt f univ u = SubderivAt f u := by
    apply subset_antisymm
    Â· intro p pin
      rw [SubderivWithinAt] at pin
      rw [â† mem_SubderivAt, â† hasSubgradientWithinAt_univ]
      apply pin
    Â· intro p pin
      rw [â† mem_SubderivAt, â† hasSubgradientWithinAt_univ] at pin;
      rw [SubderivWithinAt]
      apply pin

theorem prox_iff_subderiv (f : E â†’ â„) (hfun : ConvexOn â„ univ f) :
    âˆ€ u : E, prox_prop f x u â†” x - u âˆˆ SubderivAt f u := by
  intro u; rw [prox_prop, â† HasSubgradientAt_zero_iff_isMinOn, mem_SubderivAt]
  let g := fun u â†¦ â€–u - xâ€– ^ 2 / 2
  show 0 âˆˆ SubderivAt (f + g) u â†” x - u âˆˆ SubderivAt f u
  rw [â† SubderivAt.add]
  have subg : SubderivAt g u = {u - x} := by
    let g' := fun u â†¦ u - x
    have gderiv : âˆ€ x, HasGradientAt g (g' x) x := gradient_of_sq
    have : SubderivWithinAt g univ u = {g' u} := by
      apply SubderivWithinAt_eq_gradient; simp
      have gconv : ConvexOn â„ univ g := by
        rcases hfun with âŸ¨conv, _âŸ©
        apply convex_of_norm_sq
        apply conv
      apply gconv; apply gderiv
    rw [â† Subderivat_eq_SubderivWithinAt_univ, this]
  rw [subg]; simp

theorem prox_iff_grad (f : E â†’ â„) {f' : E â†’ E} (hfun : ConvexOn â„ univ f)
    (hdiff : âˆ€ x, HasGradientAt f (f' x) x) :
    âˆ€ u : E, prox_prop f x u â†” f' u = x - u := by
  intro u
  have iff_subderiv : âˆ€ u : E, prox_prop f x u â†” x - u âˆˆ SubderivAt f u := by
    apply prox_iff_subderiv
    apply hfun
  specialize iff_subderiv u
  rw [iff_subderiv, â† Subderivat_eq_SubderivWithinAt_univ]
  have subderiv_eq_gradient : SubderivWithinAt f univ u = {f' u} := by
    apply SubderivWithinAt_eq_gradient; simp
    apply hfun; apply hdiff
  rw [subderiv_eq_gradient]; simp
  apply eq_comm

theorem prox_iff_grad_smul (f : E â†’ â„) {f' : E â†’ E} (t : â„) (hfun : ConvexOn â„ univ f)
    (hdiff : âˆ€ x, HasGradientAt f (f' x) x) (tnonneg : 0 â‰¤ t) :
    âˆ€ u : E, prox_prop (t â€¢ f) x u â†” t â€¢ f' u = x - u := by
  intro u
  let g := fun u â†¦ (t â€¢ f) u
  let g' := fun u â†¦ t â€¢ f' u
  have gconv : ConvexOn â„ univ g := by
    apply ConvexOn.smul tnonneg hfun
  have gderiv : âˆ€ x, HasGradientAt g (g' x) x := by
    intro s
    specialize hdiff s
    rw [HasGradient_iff_Convergence_Point] at hdiff
    apply Convergence_HasGradient
    by_cases h : t > 0
    Â· simp; intro e ep
      have eq1 : 0 < tâ»Â¹ * e := by
        field_simp; apply div_pos ep h
      specialize hdiff (tâ»Â¹ * e) eq1
      rcases hdiff with âŸ¨delta, dpos, cvgâŸ©
      use delta; simp at h; use dpos
      intro z zled
      specialize cvg z zled; field_simp at cvg
      rw [inner_smul_left]; simp; rw [sub_sub, â† mul_add, â† mul_sub, abs_mul, abs_of_pos h]
      rw [â† le_div_iff' h, â† sub_sub]; exact cvg
    Â· have h0 : t = (0 : â„) := by linarith
      simp; intro e ep; use e; use ep
      intro z _; rw [h0]; simp; apply mul_nonneg
      linarith; apply norm_nonneg
  rw [prox_iff_subderiv]
  have : SubderivWithinAt g univ u = {g' u} := by
      apply SubderivWithinAt_eq_gradient; simp
      apply gconv; apply gderiv
  rw [â† Subderivat_eq_SubderivWithinAt_univ, this]; simp
  apply eq_comm
  apply gconv

theorem prox_iff_subderiv_smul (f : E â†’ â„) {t : â„} (hfun : ConvexOn â„ univ f) (ht : 0 < t) :
    âˆ€ u : E, prox_prop (t â€¢ f) x u â†” (1 / t) â€¢ (x - u) âˆˆ (SubderivAt f u) := by
  intro u
  let g := fun u â†¦ (t â€¢ f) u
  have tnonneg : 0 â‰¤ t := by linarith
  have gconv : ConvexOn â„ univ g := by
    apply ConvexOn.smul tnonneg hfun
  rw [prox_iff_subderiv, â† mem_SubderivAt, â† mem_SubderivAt]
  rw [HasSubgradientAt, HasSubgradientAt]
  constructor
  Â· intro cond y
    specialize cond y; simp at cond
    rw [inner_smul_left]; simp
    rw [â† mul_le_mul_left ht]; ring_nf; field_simp
    exact cond
  Â· intro cond y
    specialize cond y; rw [inner_smul_left] at cond; field_simp at cond
    simp
    have hrect : 0 < tâ»Â¹ := by
      simp; linarith
    rw [â† mul_le_mul_left hrect]; ring_nf; field_simp
    exact cond
  exact gconv

end properties

end
