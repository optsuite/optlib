/-
Copyright (c) 2024 Wanyi He. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Author: Wanyi He, Chenyi Li
-/
import Mathlib.Topology.MetricSpace.Basic
import Mathlib.Topology.Basic
import Mathlib.Order.Filter.Extr
import Mathlib.Analysis.Calculus.Gradient.Basic
import Mathlib.Analysis.Calculus.LineDeriv.Basic
import Mathlib.Data.Real.Sign
import Optlib.Convex.BanachSubgradient
import Optlib.Convex.ConvexFunction

/-!
# Subgradient of convex functions

The file defines subgradient for convex functions in E and proves some basic properties.

Let `f : E ‚Üí ‚Ñù` be a convex function on `s` and `g : E`, where `s` is a set of `E`.
`g` is a subgradient of `f` at `x` if for any `y ‚àà s`, we have `f y ‚â• f x + inner g (y - x)`.
The insight comes from the first order condition of convex function.

## Main declarations

* `HasSubgradientAt f g x`: The function `f` has subgradient `g` at `x`.
* `HasSubgradientWithinAt f g s x`: The function `f` has subgradient `g` at `x` within `s`.
* `SubderivAt f x`: The subderiv of `f` at `x` is the collection of all possible
  subgradients of `f` at `x`.
* `SubderivWithinAt f s x`: The subderiv of `f` at `x` within `s` is
  the collection of all possible subgradients of `f` at `x` within `s`.

## Main results

* `SubderivWithinAt_eq_gradient`: The subderiv of differentiable convex functions
  is the singleton of its gradient.
* `HasSubgradientAt_zero_iff_isMinOn`: `0` is a subgradient of `f` at `x` if and
  only if `x` is a minimizer of `f`.
-/

open Filter Topology Set InnerProductSpace

local notation gradient "‚àá*" => (toDualMap ‚Ñù _) gradient

local notation "‚ü™" x ", " y "‚ü´" => @inner ‚Ñù _ _ x y

section

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ‚Ñù E]

variable {f : E ‚Üí ‚Ñù} {g : E} {x : E} {s : Set E}

noncomputable section

/-- Subgradient of functions --/
def HasSubgradientAt (f : E ‚Üí ‚Ñù) (g x : E) : Prop :=
  ‚àÄ y, f y ‚â• f x + ‚ü™g, y - x‚ü´

def HasSubgradientWithinAt (f : E ‚Üí ‚Ñù) (g : E) (s : Set E) (x : E) : Prop :=
  ‚àÄ y ‚àà s, f y ‚â• f x + ‚ü™g, y - x‚ü´

/-- Subderiv of functions --/
def SubderivAt (f : E ‚Üí ‚Ñù) (x : E) : Set E :=
  {g : E| HasSubgradientAt f g x}

def SubderivWithinAt (f : E ‚Üí ‚Ñù) (s : Set E) (x : E) : Set E :=
  {g : E| HasSubgradientWithinAt f g s x}

@[simp]
theorem mem_SubderivAt : HasSubgradientAt f g x ‚Üî g ‚àà SubderivAt f x := ‚ü®id, id‚ü©

@[simp]
theorem hasSubgradientWithinAt_univ :
    HasSubgradientWithinAt f g univ x ‚Üî HasSubgradientAt f g x :=
  ‚ü®fun h y => h y trivial, fun h y _ => h y‚ü©

theorem HasSubgradientAt.hasSubgradientWithinAt :
    HasSubgradientAt f g x ‚Üí HasSubgradientWithinAt f g s x := fun h y _ => h y

theorem SubderivAt_SubderivWithinAt :
    SubderivAt f x = SubderivWithinAt f univ x := by
  simp only [SubderivAt, SubderivWithinAt, hasSubgradientWithinAt_univ]

end

section

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ‚Ñù E] [CompleteSpace E]

variable {f : E ‚Üí ‚Ñù} {g : E} {x : E} {s : Set E}


theorem HasSubgradientAt_HasBanachSubgradientAt {f : E ‚Üí ‚Ñù} {g : E} {x : E} :
    HasSubgradientAt f g x ‚Üî Banach_HasSubgradientAt f (toDual ‚Ñù E g) x := by
  constructor
  ¬∑ intro h z
    unfold HasSubgradientAt at h
    obtain hz := h z
    simp; linarith
  intro h z
  unfold Banach_HasSubgradientAt at h
  obtain hz := h z; simp at hz
  linarith

theorem HasBanachSubgradientAt_HasSubgradientAt {f : E ‚Üí ‚Ñù} {g : E ‚ÜíL[‚Ñù] ‚Ñù} {x : E} :
    Banach_HasSubgradientAt f g x ‚Üî HasSubgradientAt f ((toDual ‚Ñù E).symm g) x := by
  constructor
  ¬∑ intro h z
    unfold Banach_HasSubgradientAt at h
    obtain hz := h z
    simp; rw [‚Üê ContinuousLinearMap.map_sub]; linarith
  intro h z
  unfold HasSubgradientAt at h
  obtain hz := h z; simp at hz
  rw [ContinuousLinearMap.map_sub]; linarith

theorem HasSubgradientWithinAt_HasBanachSubgradientWithinAt {f : E ‚Üí ‚Ñù} {g : E} {x : E} :
    HasSubgradientWithinAt f g s x ‚Üî Banach_HasSubgradientWithinAt f (toDual ‚Ñù E g) s x := by
  constructor
  ¬∑ intro h z hz
    unfold HasSubgradientAt at h
    obtain hz := h z hz
    simp; linarith
  intro h z hz
  unfold Banach_HasSubgradientAt at h
  obtain hz := h z hz; simp at hz
  linarith

theorem HasBanachSubgradientWithinAt_HasSubgradientWithinAt {f : E ‚Üí ‚Ñù} {g : E ‚ÜíL[‚Ñù] ‚Ñù} {x : E} :
    Banach_HasSubgradientWithinAt f g s x ‚Üî HasSubgradientWithinAt f ((toDual ‚Ñù E).symm g) s x := by
  constructor
  ¬∑ intro h z hz
    unfold Banach_HasSubgradientAt at h
    obtain hz := h z hz
    simp; rw [‚Üê ContinuousLinearMap.map_sub]; linarith
  intro h z hz
  unfold HasSubgradientAt at h
  obtain hz := h z hz; simp at hz
  rw [ContinuousLinearMap.map_sub]; linarith

end

/-! ### Congruence properties of the `Subderiv` -/
section congr

variable {f‚ÇÅ f‚ÇÇ : E ‚Üí ‚Ñù} {t s : Set E}

theorem SubderivAt.congr (h : f‚ÇÅ = f‚ÇÇ) : SubderivAt f‚ÇÅ x = SubderivAt f‚ÇÇ x := by
  ext g; rw [h]

theorem SubderivWithinAt.congr (h : ‚àÄ y ‚àà s, f‚ÇÅ y = f‚ÇÇ y) (hf : f‚ÇÅ x = f‚ÇÇ x):
    SubderivWithinAt f‚ÇÅ s x = SubderivWithinAt f‚ÇÇ s x := by
  ext g
  exact ‚ü®fun hg y ys => by rw [‚Üê h y ys, ‚Üê hf]; exact hg y ys,
            fun hg y ys => by rw [h y ys, hf]; exact hg y ys‚ü©

theorem SubderivWithinAt.congr_of_mem (xs : x ‚àà s) (h : ‚àÄ y ‚àà s, f‚ÇÅ y = f‚ÇÇ y) :
    SubderivWithinAt f‚ÇÅ s x = SubderivWithinAt f‚ÇÇ s x := congr h (h x xs)

theorem SubderivAt.congr_mono (h : ‚àÄ y ‚àà s, f‚ÇÅ y = f‚ÇÇ y) (hx : f‚ÇÅ x = f‚ÇÇ x) (h‚ÇÅ : t ‚äÜ s) :
    SubderivWithinAt f‚ÇÅ t x = SubderivWithinAt f‚ÇÇ t x :=
  SubderivWithinAt.congr (fun y a => h y (h‚ÇÅ a)) hx

/-- The subderiv of `f` at `x` is a closed set. --/
theorem SubderivAt.isClosed : ‚àÄ x, IsClosed (SubderivAt f x) := by
  intro x
  by_cases e : SubderivAt f x = ‚àÖ
  ¬∑ apply Eq.subst (Eq.symm e) isClosed_empty
  rw [‚Üê isSeqClosed_iff_isClosed]
  intro g g' hg cg y
  obtain cg' := Tendsto.const_add (f x) (Filter.Tendsto.inner cg tendsto_const_nhds)
  apply le_of_tendsto_of_tendsto' cg' tendsto_const_nhds (fun n => hg n y)

theorem SubderivWithinAt.isClosed : ‚àÄ x, IsClosed (SubderivWithinAt f s x) := by
  intro x
  by_cases e : SubderivWithinAt f s x = ‚àÖ
  ¬∑ apply Eq.subst (Eq.symm e) isClosed_empty
  rw [‚Üê isSeqClosed_iff_isClosed]
  intro g g' hg cg y ys
  obtain cg' := Tendsto.const_add (f x) (Filter.Tendsto.inner cg tendsto_const_nhds)
  apply le_of_tendsto_of_tendsto' cg' tendsto_const_nhds (fun n => hg n y ys)

/-- The subderiv of `f` at `x` is a convex set. --/
theorem SubderivAt.convex : ‚àÄ x, Convex ‚Ñù (SubderivAt f x) := by
  intro x
  by_cases e : SubderivAt f x = ‚àÖ
  ¬∑ apply Eq.subst (Eq.symm e) convex_empty
  intro g‚ÇÅ h1 g‚ÇÇ h2 a b lea leb abeq y
  have ineq1 : a ‚Ä¢ f y ‚â• a ‚Ä¢ f x + a ‚Ä¢ ‚ü™g‚ÇÅ, y - x‚ü´ := by
    rw [‚Üê smul_add]
    apply smul_le_smul_of_nonneg_left (h1 y) lea
  have ineq2 : b ‚Ä¢ f y ‚â• b ‚Ä¢ f x + b ‚Ä¢ ‚ü™g‚ÇÇ, y - x‚ü´ := by
    rw [‚Üê smul_add]
    apply smul_le_smul_of_nonneg_left (h2 y) leb
  have eq : (a ‚Ä¢ f x + a ‚Ä¢ ‚ü™g‚ÇÅ, y - x‚ü´) + (b ‚Ä¢ f x + b ‚Ä¢ ‚ü™g‚ÇÇ, y - x‚ü´)
      = f x + ‚ü™a ‚Ä¢ g‚ÇÅ + b ‚Ä¢ g‚ÇÇ, y - x‚ü´ := by
    rw [add_add_add_comm, ‚Üê Eq.symm (Convex.combo_self abeq (f x))]
    apply congrArg (HAdd.hAdd (f x))
    rw [inner_add_left, inner_smul_left, inner_smul_left]; rfl
  rw [Eq.symm (Convex.combo_self abeq (f y)), ‚Üê eq]
  apply add_le_add ineq1 ineq2

theorem SubderivWithinAt.convex : ‚àÄ x ‚àà s, Convex ‚Ñù (SubderivWithinAt f s x) := by
  intro x _
  by_cases e : SubderivWithinAt f s x = ‚àÖ
  ¬∑ apply Eq.subst (Eq.symm e) convex_empty
  intro g‚ÇÅ h1 g‚ÇÇ h2 a b lea leb abeq y ys
  have ineq1 : a ‚Ä¢ f y ‚â• a ‚Ä¢ f x + a ‚Ä¢ ‚ü™g‚ÇÅ, y - x‚ü´ := by
    rw [‚Üê smul_add]
    apply smul_le_smul_of_nonneg_left (h1 y ys) lea
  have ineq2 : b ‚Ä¢ f y ‚â• b ‚Ä¢ f x + b ‚Ä¢ ‚ü™g‚ÇÇ, y - x‚ü´ := by
    rw [‚Üê smul_add]
    apply smul_le_smul_of_nonneg_left (h2 y ys) leb
  have eq : (a ‚Ä¢ f x + a ‚Ä¢ ‚ü™g‚ÇÅ, y - x‚ü´) + (b ‚Ä¢ f x + b ‚Ä¢ ‚ü™g‚ÇÇ, y - x‚ü´)
      = f x + ‚ü™a ‚Ä¢ g‚ÇÅ + b ‚Ä¢ g‚ÇÇ, y - x‚ü´ := by
    rw [add_add_add_comm, ‚Üê Eq.symm (Convex.combo_self abeq (f x))]
    apply congrArg (HAdd.hAdd (f x))
    rw [inner_add_left, inner_smul_left, inner_smul_left]; rfl
  rw [Eq.symm (Convex.combo_self abeq (f y)), ‚Üê eq]
  apply add_le_add ineq1 ineq2

/-- Monotonicity of subderiv--/
theorem subgradientAt_mono {u v : E} {f : E ‚Üí ‚Ñù}{y : E}
    (hu : u ‚àà SubderivAt f x) (hv : v ‚àà SubderivAt f y) : ‚ü™u - v, x - y‚ü´ ‚â• (0 : ‚Ñù):= by
  specialize hu y; specialize hv x
  have ineq1 : ‚ü™u, x - y‚ü´ ‚â• f x - f y := by
    rw [congrArg (‚ü™u, ¬∑‚ü´) (Eq.symm (neg_sub y x)), inner_neg_right]; linarith
  have _ : ‚ü™v, x - y‚ü´ ‚â§ f x - f y := Iff.mpr le_sub_iff_add_le' hv
  rw [inner_sub_left]; linarith

end congr

/-! ### Basic properties about `Subderiv` -/

section Basic_properties

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ‚Ñù E] [CompleteSpace E]

variable {f : E ‚Üí ‚Ñù} {g : E} {x y : E} {s : Set E}

open Bornology

/- The subderiv of `f` at interior point `x` is a nonempty set -/
theorem SubderivWithinAt.Nonempty  (hf : ConvexOn ‚Ñù s f) (hc : ContinuousOn f (interior s)):
    ‚àÄ x ‚àà interior s, (SubderivWithinAt f s x).Nonempty := by
  intro x hx
  obtain h := Banach_SubderivWithinAt.Nonempty hf hc hx
  unfold Set.Nonempty at h ‚ä¢
  rcases h with ‚ü®g, hg‚ü©; unfold Banach_SubderivWithinAt at hg
  simp at hg
  rw [HasBanachSubgradientWithinAt_HasSubgradientWithinAt] at hg;
  use (toDual ‚Ñù E).symm g; unfold SubderivWithinAt
  simp [hg]

theorem SubderivAt.nonempty (hf : ConvexOn ‚Ñù univ f) (hc : ContinuousOn f univ) :
    ‚àÄ x, Nonempty (SubderivAt f x) := by
  intro x
  rw [SubderivAt_SubderivWithinAt]
  have : x ‚àà interior univ := by simp
  rw [‚Üê interior_univ] at hc
  obtain h := SubderivWithinAt.Nonempty hf hc x this
  simp only [nonempty_subtype]
  rcases h with ‚ü®a, ha‚ü©
  exact ‚ü®a, ha‚ü©

end Basic_properties

/-! ### Calculation of `Subderiv` -/

section equivalence

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ‚Ñù E] [CompleteSpace E]

variable {f : E ‚Üí ‚Ñù} {g : E} {x y : E} {s : Set E}

open Pointwise

/-- Subderiv of differentiable convex functions --/
theorem SubderivWithinAt_eq_gradient {f'x : E} (hx : x ‚àà interior s)
    (hf : ConvexOn ‚Ñù s f) (h : HasGradientAt f (f'x) x) :
    SubderivWithinAt f s x = {f'x} := by
  obtain h' := hasGradientAt_iff_hasFDerivAt.mp h
  let g := f'x
  rw [Set.eq_singleton_iff_nonempty_unique_mem]
  constructor
  ¬∑ use g; intro y ys
    apply Convex_first_order_condition' h hf (interior_subset hx) y ys
  intro g' hg'; by_contra neq
  apply not_le_of_gt (norm_sub_pos_iff.mpr neq)
  let v := g' - g; obtain vneq := sub_ne_zero.mpr neq
  have : Tendsto (fun (t : ‚Ñù) => (f (x + t ‚Ä¢ v) - f x - ‚ü™g, t ‚Ä¢ v‚ü´) * ‚Äñt ‚Ä¢ v‚Äñ‚Åª¬π)
      (ùìù[>] 0) (ùìù 0) := by
    rw [Metric.tendsto_nhdsWithin_nhds]; intro Œµ Œµpos
    unfold HasFDerivAt at h'
    rw [hasFDerivAtFilter_iff_tendsto, Metric.tendsto_nhds_nhds] at h'
    obtain ‚ü®Œ¥, Œ¥pos, hŒ¥‚ü© := h' Œµ Œµpos
    use (Œ¥ * ‚Äñv‚Äñ‚Åª¬π)
    obtain pos := mul_pos Œ¥pos (inv_pos.mpr (norm_pos_iff.mpr vneq))
    constructor
    ¬∑ exact pos
    intro t _ ht; rw [dist_eq_norm] at ht; rw [dist_eq_norm]
    have : dist (x + t ‚Ä¢ v) x < Œ¥ := by
      rw [dist_eq_norm, add_sub_cancel_left, norm_smul, ‚Üê (sub_zero t)]
      apply lt_of_lt_of_eq ((mul_lt_mul_right (norm_sub_pos_iff.mpr neq)).mpr ht)
      rw [mul_assoc, inv_mul_cancel‚ÇÄ (norm_ne_zero_iff.mpr vneq), mul_one]
    specialize hŒ¥ this; rw [dist_eq_norm] at hŒ¥
    have eq1 : ‚Äñ‚Äñx + t ‚Ä¢ v - x‚Äñ‚Åª¬π‚Äñ = ‚Äñt ‚Ä¢ v‚Äñ‚Åª¬π := by
      rw [add_sub_cancel_left, norm_inv, norm_norm]
    have eq2 : (g ‚àá*) (x + t ‚Ä¢ v - x) = ‚ü™g, t ‚Ä¢ v‚ü´ := by rw [add_sub_cancel_left]; exact rfl
    have eq3 : ‚Äñ(f (x + t ‚Ä¢ v) - f x - ‚ü™g, t ‚Ä¢ v‚ü´) * ‚Äñt ‚Ä¢ v‚Äñ‚Åª¬π - 0‚Äñ =
      ‚Äñf (x + t ‚Ä¢ v) - f x - ‚ü™g, t ‚Ä¢ v‚ü´‚Äñ * ‚Äñt ‚Ä¢ v‚Äñ‚Åª¬π := by
        rw [sub_zero, norm_mul, norm_inv, norm_norm]
    have eq : ‚Äñ‚Äñx + t ‚Ä¢ v - x‚Äñ‚Åª¬π * ‚Äñf (x + t ‚Ä¢ v) - f x - (g ‚àá*) (x + t ‚Ä¢ v - x)‚Äñ - 0‚Äñ =
      ‚Äñ(f (x + t ‚Ä¢ v) - f x - ‚ü™g, t ‚Ä¢ v‚ü´) * ‚Äñt ‚Ä¢ v‚Äñ‚Åª¬π - 0‚Äñ := by
        rw [sub_zero, norm_mul, norm_norm, mul_comm, eq1, eq2, ‚Üê eq3]
    apply Eq.trans_lt (Eq.symm eq) hŒ¥
  apply ge_of_tendsto this
  rw [Filter.Eventually, Metric.mem_nhdsWithin_iff]
  rw [mem_interior_iff_mem_nhds, Metric.mem_nhds_iff] at hx
  obtain ‚ü®Œµ, Œµpos, ballmem‚ü© := hx
  obtain pos := mul_pos Œµpos (inv_pos.mpr (norm_pos_iff.mpr vneq))
  use (Œµ * ‚Äñv‚Äñ‚Åª¬π); use pos; intro t ht
  have tball := ht.1; have tlt : t > 0 := ht.2
  have tvpos : ‚Äñt ‚Ä¢ v‚Äñ‚Åª¬π > 0 := by
    apply inv_pos.mpr; rw [norm_smul]
    apply smul_pos (abs_pos_of_pos tlt) (norm_sub_pos_iff.mpr neq)
  have mems : x + t ‚Ä¢ v ‚àà s := by
    apply ballmem
    rw [mem_ball_iff_norm, sub_zero] at tball
    rw [mem_ball_iff_norm, add_sub_cancel_left, norm_smul]
    have : ‚Äñt‚Äñ * ‚Äñv‚Äñ < Œµ * ‚Äñv‚Äñ‚Åª¬π * ‚Äñv‚Äñ := by
      apply (mul_lt_mul_right (norm_sub_pos_iff.mpr neq)).mpr tball
    rwa [mul_assoc, inv_mul_cancel‚ÇÄ (norm_ne_zero_iff.mpr vneq), mul_one] at this
  obtain ineq1 := hg' (x + t ‚Ä¢ v); rw [add_sub_cancel_left] at ineq1
  have eq1 : ‚Äñv‚Äñ = (‚ü™g', t ‚Ä¢ v‚ü´ - ‚ü™g, t ‚Ä¢ v‚ü´) * ‚Äñt ‚Ä¢ v‚Äñ‚Åª¬π := by
    have eq2 : ‚Äñv‚Äñ = ‚ü™v, v‚ü´ * ‚Äñv‚Äñ‚Åª¬π := by
      rw [real_inner_self_eq_norm_sq]
      apply (div_eq_iff _).mp
      ¬∑ rw [div_inv_eq_mul, pow_two]
      exact inv_ne_zero (norm_ne_zero_iff.mpr vneq)
    have eq3 : ‚ü™v, v‚ü´ * ‚Äñv‚Äñ‚Åª¬π = ‚ü™v, t ‚Ä¢ v‚ü´ * ‚Äñt ‚Ä¢ v‚Äñ‚Åª¬π := by
      have : ‚ü™v, t ‚Ä¢ v‚ü´ = ‚ü™v, v‚ü´ * t := by rw [inner_smul_right, mul_comm]
      rw [this, mul_assoc]
      have : ‚Äñv‚Äñ‚Åª¬π = t * ‚Äñt ‚Ä¢ v‚Äñ‚Åª¬π := by
        have :  t * ‚Äñt ‚Ä¢ v‚Äñ‚Åª¬π = t / ‚Äñt ‚Ä¢ v‚Äñ := rfl
        rw [this, inv_eq_one_div]
        have : t = ‚Äñt‚Äñ := by
          have : ‚Äñt‚Äñ = |t| := rfl
          rw [this, abs_of_pos tlt]
        rw [this, norm_smul, norm_norm, div_mul_eq_div_div, div_self]
        rw [norm_ne_zero_iff]
        exact ne_of_gt tlt
      rw [this]
    rw [eq2, eq3, mul_eq_mul_right_iff];
    left; rw [inner_sub_left]
  rw [mem_setOf, eq1, mul_le_mul_iff_left‚ÇÄ tvpos]
  apply sub_le_sub_right (le_sub_iff_add_le'.mpr (ineq1 mems))

/-- Alternarive version for FDeriv --/
theorem SubderivWithinAt_eq_FDeriv {f' : E ‚Üí (E ‚ÜíL[‚Ñù] ‚Ñù)} (hx : x ‚àà interior s)
    (hf : ConvexOn ‚Ñù s f) (h : HasFDerivAt f (f' x) x) :
    SubderivWithinAt f s x = {(toDual ‚Ñù E).symm (f' x)} := by
  have h‚ÇÅ : HasFDerivAt f ((toDual ‚Ñù E) ((LinearIsometryEquiv.symm (toDual ‚Ñù E)) (f' x))) x := by
      simp [h]
  obtain h' := hasGradientAt_iff_hasFDerivAt.mpr h‚ÇÅ
  apply SubderivWithinAt_eq_gradient hx hf
  exact h'

theorem SubderivAt_eq_gradient {f'x : E}
    (hf : ConvexOn ‚Ñù univ f) (h : HasGradientAt f (f'x) x) :
    SubderivAt f x = {f'x} := by
  rw [SubderivAt_SubderivWithinAt]
  apply SubderivWithinAt_eq_gradient _ hf h
  ¬∑ simp only [interior_univ, mem_univ]

end equivalence

/-! ### Optimality Theory for Unconstrained Nondifferentiable Problems -/

section optimality_theory

theorem HasSubgradientAt_zero_of_isMinOn (h : IsMinOn f univ x) : HasSubgradientAt f 0 x :=
  fun y => le_of_le_of_eq' (h trivial) (by rw [inner_zero_left, add_zero])

theorem isMinOn_of_HasSubgradentAt_zero (h : HasSubgradientAt f 0 x) : IsMinOn f univ x := by
  intro y _; specialize h y
  rwa [inner_zero_left, add_zero] at h

/-- `x'` minimize `f` if and only if `0` is a subgradient of `f` at `x'` --/
theorem HasSubgradientAt_zero_iff_isMinOn :
    HasSubgradientAt f 0 x ‚Üî IsMinOn f univ x :=
  ‚ü®isMinOn_of_HasSubgradentAt_zero, HasSubgradientAt_zero_of_isMinOn‚ü©

theorem HasSubgradientWithinAt_zero_of_isMinOn (h : IsMinOn f s x) :
    HasSubgradientWithinAt f 0 s x :=
  fun y ys => le_of_le_of_eq' (h ys) (by rw [inner_zero_left, add_zero])

theorem isMinOn_of_HasSubgradentWithinAt_zero (h : HasSubgradientWithinAt f 0 s x) :
    IsMinOn f s x := by
  intro y ys; specialize h y ys
  rwa [inner_zero_left, add_zero] at h

theorem HasSubgradientWithinAt_zero_iff_isMinOn :
    HasSubgradientWithinAt f 0 s x ‚Üî IsMinOn f s x :=
  ‚ü®isMinOn_of_HasSubgradentWithinAt_zero, HasSubgradientWithinAt_zero_of_isMinOn‚ü©

lemma SubderivAt_zero (xm : E) : 0 ‚àà SubderivAt f xm ‚Üî ‚àÄ y, f y ‚â• f xm + ‚ü™0, y - xm‚ü´:= by rfl

theorem first_order_convex_iff_subgradient (f : E ‚Üí ‚Ñù) (xm : E) : IsMinOn f univ xm ‚Üî 0 ‚àà SubderivAt f xm := by
  rw [SubderivAt_zero xm ,isMinOn_iff]
  constructor
  ¬∑ intro h y
    simp only [inner_zero_left, add_zero, ge_iff_le]
    apply h y
    simp only [mem_univ]
  ¬∑ intro h x _
    have := h x
    simp only [inner_zero_left, add_zero, ge_iff_le] at this
    exact this

end optimality_theory

/-! ### Computing `Subderiv` -/

section computation

open Pointwise

variable {f : E ‚Üí ‚Ñù} {g : E} {x : E} {s : Set E}

/-- Multiplication by a Positive Scalar--/
theorem HasSubgradientAt.pos_smul {c : ‚Ñù} (h : HasSubgradientAt f g x) (hc : 0 < c) :
    HasSubgradientAt (c ‚Ä¢ f) (c ‚Ä¢ g) x := by
  intro y; rw [inner_smul_left]
  have ineq : c * f y ‚â• c * (f x + ‚ü™g, y - x‚ü´) := (mul_le_mul_iff_right‚ÇÄ hc).mpr (h y)
  have eq : c * (f x + ‚ü™g, y - x‚ü´) = c * f x + c * ‚ü™g, y - x‚ü´ :=
    mul_add c (f x) (‚ü™g, y - x‚ü´)
  exact Eq.trans_le (id eq.symm) ineq

theorem SubderivAt.pos_smul {c : ‚Ñù} (hc : 0 < c) :
    SubderivAt (c ‚Ä¢ f) x = c ‚Ä¢ (SubderivAt f x) := by
  ext g
  constructor
  ¬∑ intro hg; use (c‚Åª¬π ‚Ä¢ g)
    constructor
    ¬∑ intro y;
      have neq : c ‚â† 0 :=  ne_of_gt hc
      calc
        f y = c‚Åª¬π * (c * f y) := (eq_inv_mul_iff_mul_eq‚ÇÄ neq).mpr rfl
        _ ‚â• c‚Åª¬π * (c * f x + ‚ü™g, y - x‚ü´) :=
          mul_le_mul_of_nonneg_left (hg y) (inv_nonneg.mpr (le_of_lt hc))
        _ = f x + ‚ü™c‚Åª¬π ‚Ä¢ g, y - x‚ü´ := by
          rw [mul_add, inner_smul_left, ‚Üê ((eq_inv_mul_iff_mul_eq‚ÇÄ neq).mpr rfl)]
          rfl
    exact smul_inv_smul‚ÇÄ (ne_of_gt hc) g
  rintro ‚ü®gg, hgg, eq‚ü©; intro y
  calc
    c * f y ‚â• c * (f x + ‚ü™gg, y - x‚ü´) := (mul_le_mul_iff_right‚ÇÄ hc).mpr (hgg y)
    _ = c * f x + c * ‚ü™gg, y - x‚ü´ := mul_add c (f x) (‚ü™gg, y - x‚ü´)
    _ = c * f x + ‚ü™c ‚Ä¢ gg, y - x‚ü´ := by
      rw [inner_smul_left]; exact rfl
    _ = c * f x + ‚ü™g, y - x‚ü´ := by rw [‚Üê eq]

/-- Subderivatives of the sum of two functions is a subset of the sum of the
subderivatives of the two functions --/
theorem SubderivAt.add_subset {f‚ÇÅ f‚ÇÇ : E ‚Üí ‚Ñù} :
    ‚àÄ (x : E), SubderivAt f‚ÇÅ x + SubderivAt f‚ÇÇ x ‚äÜ SubderivAt (f‚ÇÅ + f‚ÇÇ) x := by
  intro x y hy y'
  obtain ‚ü®y‚ÇÅ, hy‚ÇÅ, y‚ÇÇ, hy‚ÇÇ, eq‚ü© := Set.mem_add.mpr hy
  have eq' : y‚ÇÅ + y‚ÇÇ = y := eq
  have : (f‚ÇÅ + f‚ÇÇ) y' ‚â• (f‚ÇÅ x + ‚ü™y‚ÇÅ, y' - x‚ü´) + (f‚ÇÇ x + ‚ü™y‚ÇÇ, y' - x‚ü´) :=
    add_le_add (hy‚ÇÅ y') (hy‚ÇÇ y')
  rwa [add_add_add_comm, ‚Üê inner_add_left, eq'] at this

private lemma leq_tendsto_zero {a : ‚Ñù} (h : ‚àÄ t > 0, 0 < a + t) : 0 ‚â§ a := by
  by_contra h'; push_neg at h';
  specialize h (- a / 2) (by linarith)
  linarith

end computation

section

variable [CompleteSpace E]

open Pointwise

theorem SubderivAt.add {f‚ÇÅ f‚ÇÇ : E ‚Üí ‚Ñù} (h‚ÇÅ : ConvexOn ‚Ñù univ f‚ÇÅ) (h‚ÇÇ: ConvexOn ‚Ñù univ f‚ÇÇ)
    (hcon : ContinuousOn f‚ÇÅ univ) :
    ‚àÄ (x : E), SubderivAt f‚ÇÅ x + SubderivAt f‚ÇÇ x = SubderivAt (f‚ÇÅ + f‚ÇÇ) x := by
  intro x‚ÇÄ
  apply Subset.antisymm (SubderivAt.add_subset x‚ÇÄ)
  rw [SubderivAt, SubderivAt, SubderivAt, Set.subset_def]
  intro g hg
  rw [Set.mem_setOf] at hg; rw [Set.mem_add]
  let S‚ÇÅ := {(x, y) : E √ó ‚Ñù | y > f‚ÇÅ (x + x‚ÇÄ) - f‚ÇÅ x‚ÇÄ - ‚ü™g, x‚ü´}
  let S‚ÇÇ := {(x, y) : E √ó ‚Ñù | y ‚â§ f‚ÇÇ x‚ÇÄ - f‚ÇÇ (x + x‚ÇÄ)}

  have hs1 : Convex ‚Ñù S‚ÇÅ := by
    rw [convex_iff_forall_pos]; intro i hi j hj a b ha hb hab
    rw [Set.mem_setOf] at hi hj ‚ä¢; simp at hi hj ‚ä¢
    rw [‚Üê mul_lt_mul_iff_of_pos_left ha] at hi
    rw [‚Üê mul_lt_mul_iff_of_pos_left hb] at hj
    rw [inner_add_right, inner_smul_right, inner_smul_right, ‚Üê sub_sub]
    have : x‚ÇÄ = (1 : ‚Ñù) ‚Ä¢ x‚ÇÄ := by simp only [one_smul]
    have : a ‚Ä¢ i.1 + b ‚Ä¢ j.1 + x‚ÇÄ = a ‚Ä¢ (i.1 + x‚ÇÄ) + b ‚Ä¢ (j.1 + x‚ÇÄ) := by
      nth_rw 1 [this]; rw [smul_add, smul_add, ‚Üê hab, add_smul];
      rw [‚Üê add_assoc, ‚Üê add_assoc]; simp; rw [add_assoc, add_comm (b ‚Ä¢ j.1), ‚Üê add_assoc]
    have : f‚ÇÅ (a ‚Ä¢ i.1 + b ‚Ä¢ j.1 + x‚ÇÄ) ‚â§ a * f‚ÇÅ (i.1 + x‚ÇÄ) + b * f‚ÇÅ (j.1 + x‚ÇÄ) := by
      rw [this]
      rw [convexOn_iff_forall_pos] at h‚ÇÅ; rcases h‚ÇÅ with ‚ü®_, h‚ÇÅ‚ü©
      specialize @h‚ÇÅ (i.1 + x‚ÇÄ) (by trivial) (j.1 + x‚ÇÄ) (by trivial) a b ha hb hab
      simp at h‚ÇÅ; simp; linarith
    apply lt_of_lt_of_le' (b := a * f‚ÇÅ (i.1 + x‚ÇÄ) + b * f‚ÇÅ (j.1 + x‚ÇÄ) -
        f‚ÇÅ x‚ÇÄ - a * ‚ü™g, i.1‚ü´_‚Ñù - b * ‚ü™g, j.1‚ü´_‚Ñù)
    ¬∑ have : f‚ÇÅ x‚ÇÄ = (1 : ‚Ñù) ‚Ä¢ (f‚ÇÅ x‚ÇÄ) := by simp
      rw [this, ‚Üê hab, add_smul, ‚Üê sub_sub, smul_eq_mul, smul_eq_mul]; linarith
    ¬∑ simp [this];
  have hs2 : Convex ‚Ñù S‚ÇÇ := by
    rw [convex_iff_forall_pos]; intro i hi j hj a b ha hb hab
    rw [Set.mem_setOf] at hi hj ‚ä¢; simp at hi hj ‚ä¢
    rw [‚Üê mul_le_mul_iff_of_pos_left ha] at hi
    rw [‚Üê mul_le_mul_iff_of_pos_left hb] at hj
    have : x‚ÇÄ = (1 : ‚Ñù) ‚Ä¢ x‚ÇÄ := by simp only [one_smul]
    have : a ‚Ä¢ i.1 + b ‚Ä¢ j.1 + x‚ÇÄ = a ‚Ä¢ (i.1 + x‚ÇÄ) + b ‚Ä¢ (j.1 + x‚ÇÄ) := by
      nth_rw 1 [this]; rw [smul_add, smul_add, ‚Üê hab, add_smul];
      rw [‚Üê add_assoc, ‚Üê add_assoc]; simp; rw [add_assoc, add_comm (b ‚Ä¢ j.1), ‚Üê add_assoc]
    have ineq : f‚ÇÇ (a ‚Ä¢ i.1 + b ‚Ä¢ j.1 + x‚ÇÄ) ‚â§ a * f‚ÇÇ (i.1 + x‚ÇÄ) + b * f‚ÇÇ (j.1 + x‚ÇÄ) := by
      rw [this]
      rw [convexOn_iff_forall_pos] at h‚ÇÇ; rcases h‚ÇÇ with ‚ü®_, h‚ÇÇ‚ü©
      specialize @h‚ÇÇ (i.1 + x‚ÇÄ) (by trivial) (j.1 + x‚ÇÄ) (by trivial) a b ha hb hab
      simp at h‚ÇÇ; simp; linarith
    have eq : f‚ÇÇ x‚ÇÄ = a * f‚ÇÇ x‚ÇÄ + b * f‚ÇÇ x‚ÇÄ := by rw [‚Üê add_mul, hab, one_mul]
    obtain hh := sub_le_sub_left ineq (a * f‚ÇÇ x‚ÇÄ + b * f‚ÇÇ x‚ÇÄ)
    have eq' : a * (f‚ÇÇ x‚ÇÄ - f‚ÇÇ (i.1 + x‚ÇÄ)) + b * (f‚ÇÇ x‚ÇÄ - f‚ÇÇ (j.1 + x‚ÇÄ)) =
      a * f‚ÇÇ x‚ÇÄ + b * f‚ÇÇ x‚ÇÄ - (a * f‚ÇÇ (i.1 + x‚ÇÄ) + b * f‚ÇÇ (j.1 + x‚ÇÄ)) := by ring
    rw [‚Üê eq'] at hh; rw [eq]
    apply le_trans (add_le_add hi hj) hh
  have hint : Disjoint S‚ÇÅ S‚ÇÇ := by
    rw [disjoint_iff]; by_contra joint
    obtain ‚ü®‚ü®x, y‚ü©, ‚ü®hp1, hp2‚ü©‚ü© := Set.notMem_singleton_empty.mp joint
    rw [Set.mem_setOf] at hp1 hp2
    specialize hg (x + x‚ÇÄ); rw [‚Üê add_sub, sub_self, add_zero] at hg
    apply not_le_of_gt ?_ hg
    obtain ineq := add_lt_add_of_le_of_lt hp2 (neg_lt_neg hp1)
    simp at ineq
    have hh : ‚àÄ x : E, (f‚ÇÅ + f‚ÇÇ) x = f‚ÇÅ x + f‚ÇÇ x := fun x => rfl
    apply lt_of_sub_pos
    have eq : f‚ÇÇ x‚ÇÄ - f‚ÇÇ (x + x‚ÇÄ) + (‚ü™g, x‚ü´_‚Ñù - (f‚ÇÅ (x + x‚ÇÄ) - f‚ÇÅ x‚ÇÄ)) =
      f‚ÇÅ x‚ÇÄ + f‚ÇÇ x‚ÇÄ + ‚ü™g, x‚ü´_‚Ñù - (f‚ÇÅ (x + x‚ÇÄ) + f‚ÇÇ (x + x‚ÇÄ)) := by ring
    rwa [hh x‚ÇÄ, hh (x + x‚ÇÄ), ‚Üê eq]
  have hso : IsOpen S‚ÇÅ := by
    apply Continuous_epi_open (f‚ÇÅ := fun x ‚Ü¶ f‚ÇÅ (x + x‚ÇÄ) - f‚ÇÅ x‚ÇÄ - ‚ü™g, x‚ü´)
    apply ContinuousOn.sub
    ¬∑ apply ContinuousOn.sub
      ¬∑ apply ContinuousOn.comp (g := f‚ÇÅ) (f := fun x ‚Ü¶ x + x‚ÇÄ) (t := univ) hcon
        ¬∑ apply ContinuousOn.add continuousOn_id continuousOn_const
        ¬∑ simp
      apply continuousOn_const
    apply ContinuousOn.inner continuousOn_const continuousOn_id

  obtain ‚ü®f, c, ‚ü®hsl, hsr‚ü©‚ü© := geometric_hahn_banach_open hs1 hso hs2 hint
  have eq : ‚àÉ a : E, ‚àÉ b : ‚Ñù, ‚àÄ (p : E √ó ‚Ñù), f p = ‚ü™a, p.1‚ü´ + b * p.2 := by
    let f1 := ContinuousLinearMap.comp f (ContinuousLinearMap.inl ‚Ñù E ‚Ñù)
    let f2 := ContinuousLinearMap.comp f (ContinuousLinearMap.inr ‚Ñù E ‚Ñù)
    use (toDual ‚Ñù E).symm f1
    use (f2 1)
    intro p
    have hmul : (f2 1) * p.2 = f2 p.2 := by
      have h := f2.map_smul p.2 (1 : ‚Ñù)
      simpa [smul_eq_mul, mul_comm] using h.symm
    rw [hmul]; simp [f1, f2]
    have : (p.1, (0 : ‚Ñù)) + ((0 : E), p.2) = p := by simp
    nth_rw 1 [‚Üê this]; rw [ContinuousLinearMap.map_add]
  rcases eq with ‚ü®a, b, hab‚ü©

  have hin : (0, 0) ‚àà S‚ÇÇ := by rw [Set.mem_setOf]; simp
  obtain hc0 := hsr (0, 0) hin
  rw [hab (0, 0)] at hc0; simp at hc0
  have hin1 : ‚àÄ t > 0, (0, t) ‚àà S‚ÇÅ := by
    intro t ht; rw [Set.mem_setOf]; simp; linarith
  have htp : ‚àÄ t > 0, b * t < c := by
      intro t ht; obtain hin1 := hin1 t ht
      specialize hsl (0, t) hin1; specialize hab (0, t); simp at hab
      linarith
  have ceq0 : c = 0 := by
    by_contra hc; push_neg at hc; have hc : c < 0 := lt_of_le_of_ne hc0 hc
    by_cases hb : b ‚â• 0
    ¬∑ specialize htp 1 (by linarith); rw [mul_one] at htp
      linarith
    push_neg at hb
    have pos : (c / (2 * b)) > 0 := by
      apply div_pos_of_neg_of_neg hc (by linarith)
    specialize (htp (c / (2 * b)) pos); field_simp [hb] at htp
    have eq : b * c / (2 * b) = c / 2 := by
      have hb0 : b ‚â† 0 := ne_of_lt hb
      simpa [mul_comm] using (mul_div_mul_right (c) (2 : ‚Ñù) hb0)
    have eq' : b * c / (b * 2) = c / 2 := by
      simpa [mul_comm] using eq
    rw [eq'] at htp; linarith
  have bleq0 : b < 0 := by
    rw [ceq0] at htp
    specialize htp 1 (by linarith); rw [mul_one] at htp; linarith
  let hata := - (1 / b) ‚Ä¢ a

  have g1 : g + hata ‚àà {g | HasSubgradientAt f‚ÇÅ g x‚ÇÄ} := by
    rw [Set.mem_setOf]; unfold HasSubgradientAt; intro x
    simp [hata]
    have hin1 : ‚àÄ t > 0, (x - x‚ÇÄ, f‚ÇÅ x - f‚ÇÅ x‚ÇÄ - ‚ü™g, x - x‚ÇÄ‚ü´_‚Ñù + t) ‚àà S‚ÇÅ := by
      intro t ht
      rw [Set.mem_setOf]; simp; exact ht
    have ineq : ‚àÄ t > 0, 0 < b‚Åª¬π * ‚ü™a, x - x‚ÇÄ‚ü´_‚Ñù + f‚ÇÅ x - f‚ÇÅ x‚ÇÄ - ‚ü™g, x - x‚ÇÄ‚ü´_‚Ñù + t := by
      intro t ht
      specialize hsl (x - x‚ÇÄ, f‚ÇÅ x - f‚ÇÅ x‚ÇÄ - ‚ü™g, x - x‚ÇÄ‚ü´_‚Ñù + t) (hin1 t ht)
      rw [ceq0, hab (x - x‚ÇÄ, f‚ÇÅ x - f‚ÇÅ x‚ÇÄ - ‚ü™g, x - x‚ÇÄ‚ü´_‚Ñù + t)] at hsl; simp at hsl
      obtain ineq := (mul_lt_mul_left_of_neg (inv_lt_zero.mpr bleq0)).mpr hsl
      simp at ineq
      rw [mul_add, ‚Üê mul_assoc, inv_mul_cancel‚ÇÄ (ne_of_lt bleq0), one_mul] at ineq
      rw [‚Üê add_assoc, add_sub, add_sub] at ineq; exact ineq
    rw [inner_add_left, inner_neg_left, real_inner_smul_left]
    rw [‚Üê add_assoc]; simp
    have : b‚Åª¬π * ‚ü™a, x - x‚ÇÄ‚ü´_‚Ñù + f‚ÇÅ x - f‚ÇÅ x‚ÇÄ - ‚ü™g, x - x‚ÇÄ‚ü´_‚Ñù ‚â• 0 := leq_tendsto_zero ineq
    linarith
  have g2 : - hata ‚àà {g | HasSubgradientAt f‚ÇÇ g x‚ÇÄ} := by
    rw [Set.mem_setOf]; unfold HasSubgradientAt; intro x
    simp [hata];
    have hin2 : (x - x‚ÇÄ, f‚ÇÇ x‚ÇÄ - f‚ÇÇ x) ‚àà S‚ÇÇ := by rw [Set.mem_setOf]; simp
    specialize hsr (x - x‚ÇÄ, f‚ÇÇ x‚ÇÄ - f‚ÇÇ x) hin2
    rw [ceq0, hab (x - x‚ÇÄ, f‚ÇÇ x‚ÇÄ - f‚ÇÇ x)] at hsr; simp at hsr
    obtain ineq := (mul_le_mul_left_of_neg (inv_lt_zero.mpr bleq0)).mpr hsr
    rw [mul_add, ‚Üê real_inner_smul_left, mul_zero, ‚Üê mul_assoc,
        inv_mul_cancel‚ÇÄ (ne_of_lt bleq0), one_mul] at ineq
    linarith
  use (g + hata); constructor; exact g1
  use (- hata); constructor; exact g2; simp

end

/-! ### Examples of `Subderiv` -/

section

theorem SubderivAt_of_norm_at_zero : SubderivAt (fun (x : E) => ‚Äñx‚Äñ) 0 = {g | ‚Äñg‚Äñ ‚â§ 1} := by
  ext g
  constructor
  ¬∑ intro hg; by_contra h
    have hg' : ‚Äñg‚Äñ > 1 := not_le.mp h
    specialize hg g
    simp only [sub_zero, norm_zero, zero_add, real_inner_self_eq_norm_mul_norm] at hg
    have : ‚Äñg‚Äñ * ‚Äñg‚Äñ > 1 * ‚Äñg‚Äñ := mul_lt_mul_of_pos_right hg' (by linarith)
    simp only [one_mul] at this
    apply not_lt.mpr hg this
  intro hg y
  calc
    ‚Äñ(0 : E)‚Äñ + ‚ü™g, y - 0‚ü´ = ‚ü™g, y‚ü´ := by simp only [norm_zero, zero_add, sub_zero]
    _ ‚â§ ‚Äñg‚Äñ * ‚Äñy‚Äñ := real_inner_le_norm g y
    _ ‚â§ 1 * ‚Äñy‚Äñ := mul_le_mul_of_nonneg_right hg (norm_nonneg y)
    _ = ‚Äñy‚Äñ := by simp only [one_mul]

theorem SubderivAt_abs (x : ‚Ñù) :
    SubderivAt abs x = if x = 0 then Icc (-1) 1 else {Real.sign x} := by
  rw [eq_ite_iff]
  by_cases h : x = 0
  ¬∑ left;
    constructor
    ¬∑ exact h
    have h2 : Icc (-1 : ‚Ñù) 1 = {g | ‚Äñg‚Äñ ‚â§ 1} := by
      ext y; simp only [Icc, mem_setOf, norm]
      have : |y| ‚â§ 1 ‚Üî -1 ‚â§ y ‚àß y ‚â§ 1 := by apply abs_le
      exact (Iff.symm this)
    rw [h2, h]
    apply SubderivAt_of_norm_at_zero
  right
  constructor
  ¬∑ exact h
  ext g
  constructor
  ¬∑ intro hg
    by_cases hx : x > 0
    ¬∑ by_contra gne
      by_cases glt : g < 1
      ¬∑ specialize hg 0
        have hinner : ‚ü™g, -x‚ü´_‚Ñù = g * (-x) := by simp; grind
        have ineq : (0 : ‚Ñù) < 0 := by
          calc
            0 ‚â• x + g * (-x) := by
              simp only [abs_zero, zero_sub, abs_of_pos hx] at hg
              rwa [hinner] at hg
            _ = x * (1 - g) := by ring
            _ > 0 := mul_pos hx (by linarith)
        exact LT.lt.false ineq
      specialize hg (x + 1)
      have : x + 1 > 0 := by linarith
      simp only [abs_of_pos hx, abs_of_pos this, add_le_add_iff_left, add_sub_cancel_left] at hg
      apply glt
      have h1: g ‚â§ 1 := by
        calc
          g = ‚ü™g, 1‚ü´ := by simp
          _ ‚â§ 1 := hg
      simp only [Real.sign_of_pos hx] at gne
      exact Ne.lt_of_le gne h1
    have hx : x < 0 := Ne.lt_of_le h (not_lt.mp hx)
    rw [Real.sign_of_neg hx]
    by_contra gne
    by_cases glt : g < -1
    ¬∑ specialize hg (x - 1)
      have : x - 1 < 0 := by linarith
      simp only [abs_of_neg this, abs_of_neg hx] at hg
      have : -g ‚â§ 1 := by
        calc
          -g = ‚ü™g, x - 1 - x‚ü´ := by simp
          _ ‚â§ 1 := by linarith [hg]
      linarith
    specialize hg 0
    have eq1 : ‚ü™g, -x‚ü´_‚Ñù = g * (-x) := by simp; grind
    have eq2 : -x + g * -x = -x * (1 + g) := by ring
    simp only [abs_zero, zero_sub, abs_of_neg hx, eq1, eq2] at hg
    have : -x * (1 + g) > 0 := by
      apply mul_pos (by linarith)
      have : g > -1 := Ne.lt_of_le' gne (not_lt.mp glt)
      linarith
    linarith
  intro hg y
  by_cases hx : x > 0
  ¬∑ simp only [Real.sign_of_pos hx] at hg
    calc
      |x| + ‚ü™g, y - x‚ü´ = x + ‚ü™1, y - x‚ü´ := by rw [abs_of_pos hx, hg]
      _ = y := by simp
      _ ‚â§ |y| := le_abs_self y
  have hx : x < 0 := Ne.lt_of_le h (not_lt.mp hx)
  simp only [Real.sign_of_neg hx] at hg
  calc
    |x| + ‚ü™g, y - x‚ü´ = -x + ‚ü™-1, y - x‚ü´ := by rw [abs_of_neg hx, hg]
    _ = -y := by simp; ring
    _ ‚â§ |y| := neg_le_abs y

end
